plugins {
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id "me.champeau.jmh" version "0.6.5"
}

repositories {
    mavenCentral()
}

sourceSets {
    main.java.srcDirs = ['src/main/java']
    main.resources.srcDirs = ['src/main/resources']
    test.java.srcDirs = ['src/test/java']
    test.resources.srcDirs = ['src/test/resources']
}
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

compileJava {
    options.compilerArgs << "-Xlint:deprecation"
}

compileTestJava {
    options.compilerArgs << "-Xlint:deprecation"
}

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

dependencies {
    // Be sure to update dependencies in pom.xml to match
    implementation 'joda-time:joda-time:2.10.13'
    implementation 'org.slf4j:slf4j-api:1.7.33'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'

    // Upgrade to version higher than 1.4 when Collect minSDK >= 26
    implementation 'org.apache.commons:commons-csv:1.4'
    implementation 'commons-io:commons-io:2.11.0'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.6.10'

    compileOnly 'net.sf.kxml:kxml2:2.3.0'

    testImplementation 'ch.qos.logback:logback-classic:1.2.10'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'net.sf.kxml:kxml2:2.3.0'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

// Required to use fileExtensions property in checkstyle file
checkstyle {
    toolVersion = '7.6.1'
}

jar {
    baseName = 'javarosa'
    // Be sure to update version in pom.xml to match
    // snapshot release = x.x.x-SNAPSHOT
    // production release = x.x.x
    version = '3.5.0-SNAPSHOT'
    archiveName = baseName + '-' + version + '.jar'

    manifest {
        attributes 'Manifest-Version': "$jar.version"
    }
}

tasks.register('installLocal') {
    def lines = []
    new File('pom.xml').withReader { reader ->
        reader.eachLine {
            lines.add(it)
        }
    }

    def changedVersion = false
    new File('pom-local.xml').withWriter { writer ->
        lines.forEach {
            if (it.trim().startsWith('<version>') && !changedVersion) {
                writer.write('<version>local</version>')
                changedVersion = true
            } else {
                writer.write(it)
            }
        }
    }

    exec {
        executable 'mvn'
        args '-f', 'pom-local.xml', '-Dgpg.skip', 'install'
    }
}


// TODO: does not build UML diagrams
javadoc {
    failOnError = false
}

jmhJar.doFirst {
    new File("build/resources/test").mkdirs()
}

jmh {
    excludes = ["(BenchmarkTemplate)"]
    threads = 1
    fork = 1
    warmup = '2s'
    warmupIterations = 10
    warmupBatchSize = 1
    warmupForks = 1
    iterations = 20
    timeOnIteration = '2s'
    timeUnit = 's'
    benchmarkMode = ['avgt','ss']
    includeTests = true
    resultFormat = 'CSV'
    forceGC = true

    duplicateClassesStrategy = DuplicatesStrategy.WARN
}

/**
 * Returns the value of a property of this build if it exists. Otherwise,
 * returns the provided default value.
 *
 * This method is used as a safe way to get properties that might or might not
 * be defined in the gradle.properties file.
 */
String getValue(String key, String defaultValue) {
    if (this.hasProperty(key))
        return this.getProperty(key)
    return defaultValue
}

apply from: 'android-api-level-checker.gradle'
