plugins {
    id 'java'
    id 'checkstyle'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
        resources {
            srcDirs = ['resources']
        }
    }
}

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

dependencies {
    // Be sure to update dependencies in pom.xml to match
    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compileOnly group: 'net.sf.kxml', name: 'kxml2', version: '2.3.0'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'net.sf.kxml', name: 'kxml2', version: '2.3.0'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

// Required to use fileExtensions property in checkstyle file
checkstyle {
    toolVersion = '7.6.1' 
}

jar {
    baseName = 'opendatakit-javarosa'
    // Be sure to update version in pom.xml to match
    // snapshot release = x.x.x-SNAPSHOT
    // production release = x.x.x
    version = '2.9.0'
    archiveName = baseName + '-' + version + '.jar'

    manifest {
        attributes 'Manifest-Version': "$jar.version"
    }
}

// Useful for testing
task explodedJar(type: Copy) {
    into "$buildDir/libs/$jar.baseName $jar.version"
    with jar
}

// TODO: does not build UML diagrams
javadoc {
    failOnError = false
}

